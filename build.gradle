plugins {
	id "idea"
	id 'maven-publish'
	id "org.jetbrains.kotlin.jvm" version "1.6.10"
	id "edu.wpi.first.GradleRIO" version "2023.2.1"
	id "com.diffplug.spotless" version "6.3.0"
	id "com.peterabeles.gversion" version "1.10"
	id "org.jetbrains.kotlinx.kover" version "0.4.2"
}

// do not change main class (does not run without it being Main
repositories {
	mavenCentral()
	jcenter()
	maven { url "https://jitpack.io" }
}
// Defining my dependencies. In this case, WPILib (+ friends)
dependencies {
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"

	implementation "org.jetbrains.kotlin:kotlin-stdlib"

	implementation "edu.wpi.first.wpimath:wpimath-java:2023.2.1"
	implementation "edu.wpi.first.wpiutil:wpiutil-java:2023.2.1"

	implementation wpi.java.deps.wpilib()
	implementation wpi.java.vendor.java()

	// We need to add the Kotlin stdlib in order to use most Kotlin language features.
	// compile "org.jetbrains.kotlin:kotlin-stdlib"
}
test {
	useJUnitPlatform()
}

publishing {
	publications {
		release(MavenPublication) {
			groupId = 'org.team4099'
			artifactId = 'falconutils'
			version = '1.1.8'

			from(components["kotlin"])
		}
	}
}

// Disabling unwanted unit tests tasks
//project.gradle.startParameter.excludedTaskNames.add("test")
//project.gradle.startParameter.excludedTaskNames.add("koverHtmlReport")
//project.gradle.startParameter.excludedTaskNames.add("koverXmlReport")

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	kotlin {
		// by default the target is every '.kt' and '.kts` file in the java sourcesets
		target fileTree(".") {
			include "**/*.kt"
			exclude "**/BuildConstants.kt"
		}
		ktfmt()
		ktlint().userData(['max_line_length': '120', 'insert_final_newline': 'true', 'indent_size': '2', 'continuation_indent_size': '2'])
	}
}

wrapper {
	gradleVersion = '7.3.3'
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
	}
}

gversion {
	srcDir       = "src/main/kotlin/"           // path is relative to the sub-project by default
	// Gradle variables can also be used
	// E.g. "${project.rootDir}/module/src/main/java"
	classPackage = "org.team4099.lib"
	className    = "BuildConstants"                // optional. If not specified GVersion is used
	dateFormat   = "yyyy-MM-dd'T'HH:mm:ss'Z'" // optional. This is the default
	timeZone     = "EST"                      // optional. UTC is default
	debug        = false                      // optional. print out extra debug information
	language     = "kotlin"                     // optional. Can be Java, Kotlin, YAML, or Properties. Case insensitive.
	explicitType = false                      // optional. Force types to be explicitly printed
	indent       = "\t"                       // optional. Change how code is indented. 1 tab is default.
	annotate     = false                      // optional. Java only. Adds @Generated annotation
}
