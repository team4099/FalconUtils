plugins {
	id "idea"
	id 'maven-publish'
	id "org.jetbrains.kotlin.jvm" version "1.6.0"
	id "edu.wpi.first.GradleRIO" version "2026.1.1"
	id "com.diffplug.spotless" version "6.3.0"
	id "com.peterabeles.gversion" version "1.10"
	id "org.jetbrains.kotlinx.kover" version "0.4.2"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

// do not change main class (does not run without it being Main
repositories {
	mavenCentral()
	jcenter()
	maven { url "https://jitpack.io" }
}
// Defining my dependencies. In this case, WPILib (+ friends)
dependencies {
	annotationProcessor wpi.java.deps.wpilibAnnotations()
	implementation wpi.java.deps.wpilib()
	implementation wpi.java.vendor.java()

	nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
	nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
	simulationDebug wpi.sim.enableDebug()

	nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
	nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
	simulationRelease wpi.sim.enableRelease()

	testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"

	implementation "org.jetbrains.kotlin:kotlin-stdlib"

	// We need to add the Kotlin stdlib in order to use most Kotlin language features.
	// compile "org.jetbrains.kotlin:kotlin-stdlib"
}

test {
	useJUnitPlatform()
	systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

publishing {
	publications {
		release(MavenPublication) {
			groupId = 'org.team4099'
			artifactId = 'falconutils'
			version = '1.1.36.2'

			from(components["kotlin"])
		}
	}
}

// Disabling unwanted unit tests tasks
//project.gradle.startParameter.excludedTaskNames.add("test")
//project.gradle.startParameter.excludedTaskNames.add("koverHtmlReport")
//project.gradle.startParameter.excludedTaskNames.add("koverXmlReport")

spotless {
	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	kotlin {
		// by default the target is every '.kt' and '.kts` file in the java sourcesets
		target fileTree(".") {
			include "**/*.kt"
			exclude "**/BuildConstants.kt", "**/build/**"
		}
		ktfmt()
		ktlint().userData(['max_line_length': '120', 'insert_final_newline': 'true', 'indent_size': '2', 'continuation_indent_size': '2'])
	}
}

wrapper {
	gradleVersion = '7.3.3'
}

jar {
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	from sourceSets.main.allSource
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)

tasks.withType(JavaCompile) {
	options.compilerArgs.add "-XDstringConcat=inline"
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
		jvmTarget="17"
	}
}

gversion {
	srcDir       = "src/main/kotlin/"           // path is relative to the sub-project by default
	// Gradle variables can also be used
	// E.g. "${project.rootDir}/module/src/main/java"
	classPackage = "org.team4099.lib"
	className    = "BuildConstants"                // optional. If not specified GVersion is used
	dateFormat   = "yyyy-MM-dd'T'HH:mm:ss'Z'" // optional. This is the default
	timeZone     = "EST"                      // optional. UTC is default
	debug        = false                      // optional. print out extra debug information
	language     = "kotlin"                     // optional. Can be Java, Kotlin, YAML, or Properties. Case insensitive.
	explicitType = false                      // optional. Force types to be explicitly printed
	indent       = "\t"                       // optional. Change how code is indented. 1 tab is default.
	annotate     = false                      // optional. Java only. Adds @Generated annotation
}

project.compileKotlin.dependsOn(createVersionFile)
project.compileJava.dependsOn(createVersionFile)
// Configure jar and deploy tasks
wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)
